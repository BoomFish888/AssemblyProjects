@ Filename: Lab1Set8.s
@ Author:   Kevin Preston
@ Purpose:  CS413 Lab 1 Set 8 solution. 
@ 
@ History: 
@    Date       Purpose of change
@    ----       ----------------- 
@   7-Sep-2021  Init version. Not by best work. 
@
@ Use these commands to assemble, link, run and debug this program:
@    as -o Lab1Set8.o Lab1Set8.s
@    gcc -o Lab1Set8 Lab1Set8.o
@    ./Lab1Set8 ;echo $?
@    gdb --args ./Lab1Set8 


.equ READERROR, 0 @Used to check for scanf read error. 

.global main @ Have to use main because of C library uses. 

main:

  mov r0, #1 @ for i =1
  ldr r1, =array1
  ldr r2, =array2
  ldr r3, =sumarray

foriloop:
  cmp r0, #11
  bge exitfori

  ldr r5, [r1],#4
  ldr r6, [r2],#4
  add r5, r5, r6
  str r5, [r3],#4
  add r0, #1
  b foriloop

exitfori:

@******************
printarray1:
   ldr r0, =strArray1 @ 
   bl  printf           
   ldr r1, =array1
   push {r1}
   bl printarray
  
printarray2:
   ldr r0, =strArray2 @ 
   bl  printf  
   ldr r1, =array2
   push {r1}
   bl printarray
  
printarray3:
   ldr r0, =strArray3 @ 
   bl  printf  
   ldr r1, =sumarray
   push {r1}
   bl printarray

@*******************
prompt:
@*******************

@ Ask the user to enter a number.
 
   ldr r0, =strInputPrompt @ Put the address of my string into the first parameter
   bl  printf              @ Call the C printf to display input prompt. 
 
@*******************
get_input:
@*******************

@ Set up r0 with the address of input pattern.
@ scanf puts the input value at the address stored in r1. We are going
@ to use the address for our declared variable in the data section - intInput. 
@ After the call to scanf the input is at the address pointed to by r1 which 
@ in this case will be intInput. 

   ldr r0, =numInputPattern @ Setup to read in one number.
   ldr r1, =intInput        @ load r1 with the address of where the
                            @ input value will be stored. 
   bl  scanf                @ scan the keyboard.
   cmp r0, #READERROR       @ Check for a read error.
   beq readerror            @ If there was a read error go handle it. 
   ldr r1, =intInput        @ Have to reload r1 because it gets wiped out. 
   ldr r7, [r1]             @ Read the contents of intInput and store in r1 so that
                            @ it can be printed. 

@ r7 contains the value input to keyboard. 

@ Print positive, negative or zero in sumarray

  mov r6, #1 @ for i =1
  ldr r5, =sumarray

forjloop:
  ldr r1, [r5], #4  
  cmp r6, #11  @completed the loop exit
  bge exitforj
  
  cmp r7, #1  @ Print positive only.
  bne chkzero
  cmp r1, #0 
  bgt printthis
  b   nextj

chkzero:
  cmp r7, #2 @ Print zero only.
  bne chkneg
  cmpeq r1, #0
  beq printthis
  b   nextj

chkneg:
  cmp r7, #3 @ Print negative only. 
  bne nextj  @should not need this?
  cmp r1, #0
  bmi printthis
  b nextj

printthis:
  ldr r0, =strOutputNum
  bl  printf

nextj:
  add r6, r6, #1
  b forjloop

exitforj:

   b   myexit @ leave the code. 

@@@@@@@@@@@@@
printarray:
@@@@@@@@@@@@@
@ Subroutine to print the 10 array elements 
  pop  {r5} @starting address of the array to print
  push {lr} @ Save the return address. 
  ldr  r1, [r5] @ get the starting value into r1
  mov  r6, #1   @loop counter
  
printloop:
   cmp r6, #11
   bge return
   ldr r1, [r5]
   ldr r0, =strOutputNum
   bl  printf
   add r5, r5, #4 @ go to the next word. 
   add r6, r6, #1
   b printloop
   
return:
   pop {pc} @ return from the subroutine.



@***********
readerror:
@***********
@ Got a read error from the scanf routine. Clear out the input buffer then
@ branch back for the user to enter a value. 
@ Since an invalid entry was made we now have to clear out the input buffer by
@ reading with this format %[^\n] which will read the buffer until the user 
@ presses the CR. 

   ldr r0, =strInputPattern
   ldr r1, =strInputError   @ Put address into r1 for read.
   bl scanf                 @ scan the keyboard.
@  Not going to do anything with the input. This just cleans up the input buffer.  
@  The input buffer should now be clear so get another input.

   b prompt

@*******************
myexit:
@*******************
@ End of my code. Force the exit and return control to OS

   mov r7, #0x01 @ SVC call to exit
   svc 0         @ Make the system call. 

.data

@ Declare the strings and data needed

.balign 4
array1: .word 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 @ 10 words.

.balign 4
array2: .word 1,-2,-4, 4, 1, -12,-7, 1, 1,  1 @ 10 words.

.balign 4
sumarray: .skip 10*4 @ 10 words

.balign 4
strInputPrompt: .asciz "Select 1 for Postive, 2 for Zero, 3 for Negative. \n"

.balign 4
strArray1: .asciz "Array 1 \n"
.balign 4
strArray2: .asciz "Array 2 \n"
.balign 4
strArray3: .asciz "Array 3 \n"

.balign 4
strOutputNum: .asciz " %d \n"

@ Format pattern for scanf call.

.balign 4
numInputPattern: .asciz "%d"  @ integer format for read. 

.balign 4
strInputPattern: .asciz "%[^\n]" @ Used to clear the input buffer for invalid input. 

.balign 4
strInputError: .skip 100*4  @ User to clear the input buffer for invalid input. 

.balign 4
intInput: .word 0   @ Location used to store the user input. 

@ Let the assembler know these are the C library functions. 

.global printf
@  To use printf:
@     r0 - Contains the starting address of the string to be printed. The string
@          must conform to the C coding standards.
@     r1 - If the string contains an output parameter i.e., %d, %c, etc. register
@          r1 must contain the value to be printed. 
@ When the call returns registers: r0, r1, r2, r3 and r12 are changed. 

.global scanf
@  To use scanf:
@      r0 - Contains the address of the input format string used to read the user
@           input value. In this example it is numInputPattern.  
@      r1 - Must contain the address where the input value is going to be stored.
@           In this example memory location intInput declared in the .data section
@           is being used.  
@ When the call returns registers: r0, r1, r2, r3 and r12 are changed.
@ Important Notes about scanf:
@   If the user entered an input that does NOT conform to the input pattern, 
@   then register r0 will contain a 0. If it is a valid format
@   then r0 will contain a 1. The input buffer will NOT be cleared of the invalid
@   input so that needs to be cleared out before attempting anything else.
@
@ Additional notes about scanf and the input patterns:
@    1. If the pattern is %s or %c it is not possible for the user input to generate
@       and error code. Anything that can be typed by the user on the keyboard
@       will be accepted by these two input patterns. 
@    2. If the pattern is %d and the user input 12.123 scanf will accept the 12 as
@       valid input and leave the .123 in the input buffer. 
@    3. If the pattern is "%c" any white space characters are left in the input
@       buffer. In most cases user entered carrage return remains in the input buffer
@       and if you do another scanf with "%c" the carrage return will be returned. 
@       To ignore these "white" characters use " $c" as the input pattern. This will
@       ignore any of these non-printing characters the user may have entered.
@

@ End of code and end of file. Leave a blank line after this.
